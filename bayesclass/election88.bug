# Bugs model for election88 model
#
# Source: http://www.stat.columbia.edu/~gelman/arm/examples/election88/election88.M2.bug
# (with some additional comments)
#

model {
  for (i in 1:n) { 			# loop across observations
    y[i] ~ dbin (p.bound[i], 1)         # likelihood
    p.bound[i] <- max(0, min(1, p[i]))  # avoid potential precision problems
    logit(p[i]) <- Xbeta[i]             # logit link
    Xbeta[i] <- b.0 + 			# with linear mean
      b.female * female[i] + 
      b.black * black[i] +
      b.female.black * female[i] * black[i] +
      a.age[age[i]] + 
      a.edu[edu[i]] + 
      a.age.edu[age[i],edu[i]] +
      a.state[state[i]]                 # NB: here is the state effect!
  }

  b.0 ~ dnorm (0, .0001)             	# priors on intercept and slopes
  b.female ~ dnorm (0, .0001)           # NB: second term in normal distribution is precision (as opposed to variance)
  b.black ~ dnorm (0, .0001)
  b.female.black ~ dnorm (0, .0001)

  for (j in 1:n.age) {			# priors on age slopes 
      a.age[j] ~ dnorm(0, tau.age)
      }
  for (j in 1:n.edu) {			# and education slopes 
      a.edu[j] ~ dnorm(0, tau.edu)
      } 
  for (j in 1:n.age) {			# and age cross education slopes (there are n.age \times n.educ categories here)
      for (k in 1:n.edu) {
      	  a.age.edu[j,k] ~ dnorm(0, tau.age.edu)
	  }
      }

  for (j in 1:n.state) {		# state level loop
    a.state[j] ~ dnorm(a.state.hat[j], tau.state)                # This specifies distribution for state effects
    a.state.hat[j] <- a.region[region[j]] + b.v.prev*v.prev[j]   # and here is the mean
    }
  b.v.prev ~ dnorm (0, .0001) 

  for (j in 1:n.region) {		# region priors
      a.region[j] ~ dnorm(0, tau.region)
      } 

  tau.age <- pow(sigma.age, -2)		# variance/precision conversions
  tau.edu <- pow(sigma.edu, -2) 	# NB: not necessary but keeps things 
  tau.age.edu <- pow(sigma.age.edu, -2) # in standard notation.
  tau.state <- pow(sigma.state, -2)     # In other words, parameterize everything in terms of
  tau.region <- pow(sigma.region, -2)   # tau (precision) and then parameterize tau
  	     			        # in terms of sigma since we are used to standard deviation/variance

  sigma.age ~ dunif (0, 100)    	# priors on standard deviations
  sigma.edu ~ dunif (0, 100)
  sigma.age.edu ~ dunif (0, 100)
  sigma.state ~ dunif (0, 100)
  sigma.region ~ dunif (0, 100)

}
